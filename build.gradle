buildscript {
  repositories {
    //maven {
    //  url 'https://oss.sonatype.org/content/repositories/snapshots/'
    //}
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.0'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
  //maven {
  //  url 'https://oss.sonatype.org/content/repositories/snapshots/'
  //}
  mavenCentral()
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.0'

  dexOptions {
    // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isTravis
  }

  signingConfigs {
    u2020 {
      storeFile file('u2020.keystore')
      storePassword 'android'
      keyAlias 'android'
      keyPassword 'android'
    }
  }

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 23

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    signingConfig signingConfigs.u2020

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

    testInstrumentationRunner "com.jakewharton.u2020.U2020TestRunner"
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
    }
  }

  productFlavors {
    internal {
      applicationId 'com.jakewharton.u2020.internal'
    }
    production {
      applicationId 'com.jakewharton.u2020'
    }
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    fatal 'UnusedResources'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  packagingOptions {
    exclude 'LICENSE.txt'
  }
}

// TODO remove eventually: http://b.android.com/162285
configurations {
  internalDebugCompile
}

configurations.all {
  resolutionStrategy {
    force 'com.android.support:support-annotations:23.0.1'
  }
}

dependencies {
  compile 'com.android.support:support-v4:23.0.1'
  compile 'com.android.support:support-annotations:23.0.1'
  compile 'com.android.support:appcompat-v7:23.0.1'
  compile 'com.android.support:recyclerview-v7:23.0.1'
  compile 'com.android.support:design:23.0.1'

  compile 'com.squareup.dagger:dagger:1.2.2'
  apt 'com.squareup.dagger:dagger-compiler:1.2.2'

  compile 'com.squareup.okhttp:okhttp:2.4.0'
  compile 'com.squareup.picasso:picasso:2.5.2'

  compile 'com.squareup.retrofit:retrofit:2.0.0-beta1'
  compile 'com.squareup.retrofit:converter-moshi:2.0.0-beta1'
  compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta1'
  internalDebugCompile 'com.squareup.retrofit:retrofit-mock:2.0.0-beta1'
  internalDebugCompile 'com.squareup.retrofit:adapter-rxjava-mock:2.0.0-beta1'

  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.jakewharton.timber:timber:3.0.2'
  compile 'com.jakewharton.byteunits:byteunits:0.9.0'
  internalDebugCompile 'com.jakewharton.madge:madge:1.1.1'
  internalDebugCompile 'com.jakewharton.scalpel:scalpel:1.1.1'
  internalDebugCompile 'com.jakewharton:process-phoenix:1.0.0'

  internalCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
  productionCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'

  compile 'io.reactivex:rxjava:1.0.14'
  compile 'io.reactivex:rxandroid:1.0.1'

  compile 'com.jakewharton.threetenabp:threetenabp:1.0.1'

  internalCompile 'com.mattprecious.telescope:telescope:1.4.0@aar'

  internalDebugCompile 'com.f2prateek.rx.preferences:rx-preferences:1.0.0'

  androidTestCompile 'junit:junit:4.12'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
  // TODO https://code.google.com/p/android-test-kit/issues/detail?id=157
  //androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2'
  androidTestCompile 'com.android.support.test:runner:0.3'
  androidTestCompile 'com.android.support.test:rules:0.3'

  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.27'
}

def installAll = tasks.create('installAll')
installAll.description = 'Install all applications.'
android.applicationVariants.all { variant ->
  installAll.dependsOn(variant.install)
  // Ensure we end up in the same group as the other install tasks.
  installAll.group = variant.install.group
}

// The default 'assemble' task only applies to normal variants. Add test variants as well.
android.testVariants.all { variant ->
  tasks.getByName('assemble').dependsOn variant.assemble
}
