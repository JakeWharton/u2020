buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.7.+'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.0.+'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'android'
apply plugin: 'android-apt'
apply plugin: 'hugo'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.android.support:support-v4:19.0.+'

  compile 'com.squareup.dagger:dagger:1.2.0'
  apt 'com.squareup.dagger:dagger-compiler:1.2.0'

  compile 'com.squareup.okhttp:okhttp:1.3.0'
  compile 'com.squareup.picasso:picasso:2.1.1'
  compile 'com.squareup.retrofit:retrofit:1.3.0'
  debugCompile 'com.squareup.retrofit:retrofit-mock:1.3.0'

  compile 'com.jakewharton:butterknife:4.0.1'
  compile 'com.jakewharton.timber:timber:2.1.0'
  debugCompile 'com.jakewharton.madge:madge:1.1.1'
  debugCompile 'com.jakewharton.scalpel:scalpel:1.0.0'

  compile 'com.netflix.rxjava:rxjava-core:0.16.1'
  compile 'com.netflix.rxjava:rxjava-android:0.16.1'

  compile 'com.etsy.android.grid:library:1.0.3'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 19
  buildToolsVersion "19.0.1"

  dexOptions {
    // Skip pre-dexing when running on Travis CI.
    preDexLibraries = !"true".equals(System.getenv("TRAVIS"))
  }

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 19

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  buildTypes {
    debug {
      packageNameSuffix '.dev'
      versionNameSuffix '-dev'
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}
