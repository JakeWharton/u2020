buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.13.3'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  mavenLocal()
}

dependencies {
  compile 'com.android.support:support-v4:21.0.0'

  compile 'com.google.dagger:dagger:2.0-SNAPSHOT'
  apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'
  provided 'org.glassfish:javax.annotation:10.0-b28'

  compile 'com.squareup.okhttp:okhttp:1.3.0'
  compile 'com.squareup.picasso:picasso:2.2.0'
  compile 'com.squareup.retrofit:retrofit:1.4.1'
  debugCompile 'com.squareup.retrofit:retrofit-mock:1.4.1'

  compile 'com.jakewharton:butterknife:6.0.0'
  compile 'com.jakewharton.timber:timber:2.2.2'
  debugCompile 'com.jakewharton.madge:madge:1.1.1'
  debugCompile 'com.jakewharton.scalpel:scalpel:1.1.1'

  compile 'com.netflix.rxjava:rxjava-core:0.16.1'
  compile 'com.netflix.rxjava:rxjava-android:0.16.1'

  compile 'com.etsy.android.grid:library:1.0.3'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  compileSdkVersion 21
  buildToolsVersion "21.0.2"

  dexOptions {
    // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isTravis
  }

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 21

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}
